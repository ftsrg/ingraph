plugins {
  id 'com.jfrog.bintray' version '1.7.1' apply false
}

allprojects {
  group = 'ingraph'
  version = '0.2.0'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'maven-publish'

  jar {
    from sourceSets.main.allJava
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    jcenter()
    maven { url 'https://repo.eclipse.org/content/groups/acceleo' }
    maven { url 'https://build.incquerylabs.com/nexus/content/repositories/viatra.staging/' }
    maven { url 'http://docs.inf.mit.bme.hu/ingraph-deps' }
  }

  configurations.all { exclude group: 'asm' }

  buildscript {
    repositories {
      jcenter()
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
      }
    }
  }

  bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    override = true
    publications = ['mavenJava']
  }
}

configure(subprojects - project(':ingraph-relalg-model')) {
  eclipse.classpath.file {
    whenMerged { classpath ->
      classpath.entries = classpath.entries.findAll { entry ->
        !entry.path.contains('build/') && !entry.path.contains('xtend-gen')
      }
    }
  }
}

ext {
  commonsIoVersion = '2.5'
  commonsMathVersion = '3.6.1'
  emfVersion = '2.10.1'
  guavaVersion = '21.0'
  jmhVersion = '1.17.5'
  junitVersion = '4.12'
  log4jVersion = '1.2.17'
  pegdownVersion = '1.6.0'
  scalaShortVersion = '2.11'
  scalaVersion = '2.11.8'
  scalaTestVersion = '3.0.1'
  slizaaVersion = '1.0.0-SNAPSHOT'
  viatraMinorVersion = '.0.M1'
  viatraDseVersion = '0.16' + viatraMinorVersion
  viatraVersion = '1.6' + viatraMinorVersion
  xtendVersion = '2.10.0'
  xtextVersion = xtendVersion
}

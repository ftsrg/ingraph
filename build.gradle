allprojects {
  group = 'ingraph'
  version = '0.4.0-SNAPSHOT'
}

ext {
  akkaVersion = '2.4.20'
  commonsExecVersion = '1.3'
  commonsIoVersion = '2.5'
  commonsMathVersion = '3.6.1'
  guavaVersion = '22.0'
  junitVersion = '4.12'
  log4jVersion = '1.2.17'
  neo4jDriverVersion = '1.4.6'
  neo4jVersion = '3.4.0'
  neo4jApocVersion = '3.4.0.1'
  pegdownVersion = '1.6.0'
  scalaShortVersion = '2.11'
  scalaTestVersion = '3.0.1'
  scalaVersion = '2.11.12'
  sparkVersion = '2.3.0'
  slizaaVersion = '1.0.0-SNAPSHOT'
  xtextVersion = '2.10.0'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'scala'
  apply plugin: 'maven-publish'

  jar {
    from sourceSets.main.allJava
    from sourceSets.main.allScala
  }

  sourceSets.test.resources.srcDirs = ["$rootDir/queries", "$rootDir/graphs"]

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    jcenter()
    maven { url 'https://repo.eclipse.org/content/groups/acceleo' }
    maven { url 'https://repo.eclipse.org/content/groups/viatra2' }
    maven { url 'https://docs.inf.mit.bme.hu/ingraph-deps' }
  }

  dependencies {
    compile group: 'org.scala-lang', name: 'scala-library', version: scalaVersion
    testCompile group: 'org.scalatest', name: "scalatest_$scalaShortVersion", version: scalaTestVersion
    testRuntime group: 'org.pegdown', name: 'pegdown', version: pegdownVersion
  }

  configurations.all { exclude group: 'asm' }

  buildscript {
    repositories {
      jcenter()
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
      }
    }
  }
}

grammar ingraph.report.featuregrammar.Feature with org.eclipse.xtext.common.Terminals hidden(WS, SL_COMMENT)

generate feature "http://www.report.ingraph/Feature"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Feature:
	tags+=TAG*
	name=FEATURE_TEXT
	elements+=NarrativeElement*
	scenarios+=AbstractScenario*
;

NarrativeElement:
	InOrderTo | AsA | IWantTo | FreeText
;

FreeText:
	name = TEXT
;

AsA:
	name=AS_A
;

InOrderTo:
	name=IN_ORDER_TO
;

IWantTo:
	name=I_WANT_TO
;

AbstractScenario:
	Background | Scenario | ScenarioWithOutline
;

Scenario:
	tags+=TAG* 
	name=SCENARIO_TEXT
	elements+=NarrativeElement*
	(steps += Step)+
;

ScenarioWithOutline:
	tags+=TAG* 
	name=SCENARIO_OUTLINE_TEXT
	elements+=NarrativeElement*
	(steps += Step)+
	example=Example
;

Example:
	EXAMPLE_HEADING
	heading = ExampleRow
	(rows += ExampleRow)*
;

ExampleRow:
	cells+=ExampleCell+ EXAMPLE_ROW_END
;

ExampleCell:
	value=EXAMPLE_CELL
;

Background:
	tags+=TAG* 
	name=BACKGROUND_TEXT
	(steps += Step)*
;

Step:
	GivenStep | WhenStep | ThenStep | AndStep
;

GivenStep:
	tags+=TAG* text=GIVEN_TEXT desc=OptionalText
;

WhenStep:
	tags+=TAG* text=WHEN_TEXT desc=OptionalText
;

ThenStep:
	tags+=TAG* text=THEN_TEXT desc=OptionalText (rows += ExampleRow)*
;

AndStep:
	text=AND_TEXT desc=OptionalText
;

OptionalText returns ecore::EString:
	TEXT | CODE
;

terminal EXAMPLE_HEADING: 'Examples' SPACES ':' SPACES '\r'? '\n';

terminal fragment NNL: !('\r' | '\n');
terminal fragment NL: '\r'? '\n';
terminal fragment SPACES: (' '|'\t')*;
terminal FEATURE_TEXT: ('Narrative:' | 'Feature:') NNL* NL;

terminal IN_ORDER_TO : 'In order to'  NNL* NL;
terminal AS_A : 'As a' NNL* NL;
terminal I_WANT_TO : 'I want to ' NNL* NL;

terminal SCENARIO_TEXT: 'Scenario' SPACES ':' NNL* NL;
terminal SCENARIO_OUTLINE_TEXT: 'Scenario' SPACES 'Outline' SPACES ':' NNL* NL;
terminal BACKGROUND_TEXT: 'Background' SPACES ':' NNL* NL;

terminal WHEN_TEXT: 'When ' NNL* NL;
terminal THEN_TEXT: 'Then ' NNL* NL;
terminal GIVEN_TEXT: 'Given ' NNL* NL;
terminal AND_TEXT: 'And ' NNL* NL;

terminal EXAMPLE_ROW_END: '|' SPACES NL;
terminal EXAMPLE_CELL: '|' !('\r' | '\n' | '|')+;

terminal CODE: '"""' -> '"""' | "'''" -> "'''";

terminal TAG: '@' NNL+ NL;

terminal SL_COMMENT: '#' NNL* NL;

terminal TEXT: !('|'|' '|'\t'|'\r'|'\n'|'@') NNL* NL;
terminal WS  : (' '|'\t'|'\r'|'\n')+;
